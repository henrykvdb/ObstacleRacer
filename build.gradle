import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application' version '8.1.2' apply false
    id 'com.android.library' version '8.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
    id 'com.google.gms.google-services' version "4.3.15" apply false
    id 'com.google.firebase.crashlytics' version "2.9.7" apply false
}

allprojects {
    apply plugin: "eclipse"

    version = '1.0'
    ext {
        appName = "ObstacleRacer"
        gdxVersion = '1.12.0'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
    }
}

project(":desktop") {
    apply plugin: "kotlin"


    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        api "org.jetbrains.kotlin:kotlin-stdlib:1.9.10"
    }
}

project(":android") {
    apply plugin: 'com.android.application'
    apply plugin: 'org.jetbrains.kotlin.android'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    configurations { natives }

    dependencies {
        implementation project(":core")

        implementation 'androidx.core:core-ktx:1.10.1' // Next version requires API target 34
        implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.10')

        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

        implementation(platform('com.google.firebase:firebase-bom:32.3.1'))
        implementation 'com.google.firebase:firebase-crashlytics-ktx'
        implementation 'com.google.firebase:firebase-analytics-ktx'

        implementation 'com.google.android.ump:user-messaging-platform:2.1.0'
        implementation 'com.google.android.gms:play-services-ads:22.4.0'
        implementation 'com.google.android.gms:play-services-auth:20.7.0'
        implementation 'com.google.android.gms:play-services-games:23.1.0'
    }
}

project(":core") {
    apply plugin: "kotlin"

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "org.jetbrains.kotlin:kotlin-stdlib:1.9.10"
    }
}

subprojects {
    tasks.withType(KotlinCompile).tap {
        configureEach {
            kotlinOptions.jvmTarget = "1.8"
        }
    }
}
